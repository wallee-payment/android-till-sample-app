plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.compose' version "2.0.0"
}


android {
    compileSdkVersion 35

    defaultConfig {
        applicationId 'com.wallee.android.till.sample.till'
        minSdkVersion 21
        targetSdkVersion 35
        versionCode 7
        versionName "1.9-${getCurrentGitCommitSha()}"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
	
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures{
        viewBinding true
        compose = true
    }

    composeOptions {
        kotlinCompilerExtensionVersion "2.0.21"
    }
    namespace 'com.wallee.android.till.sample.till'
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.core:core:1.6.0'
    implementation 'androidx.activity:activity:1.8.0'

    if (getCurrentGitBranch().equalsIgnoreCase("master")) {
        implementation 'com.wallee.android.till:sdk:2.0.0'
    } else {
        implementation 'com.wallee.android.till:sdk-dev:2.0.0-SNAPSHOT'
    }

    def composeBom = platform('androidx.compose:compose-bom:2025.09.00')
    implementation composeBom
    androidTestImplementation composeBom

    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.material:material'
    implementation 'androidx.compose.foundation:foundation'
    implementation 'androidx.compose.ui:ui'
    implementation "androidx.activity:activity-compose:1.9.3"
    implementation 'androidx.compose.ui:ui-tooling-preview'
    debugImplementation "androidx.compose.ui:ui-tooling"
    implementation "androidx.compose.material3:material3"
    implementation "androidx.compose.material:material-icons-core"
    implementation "androidx.compose.material:material-icons-extended"


}

def getCommandResult(cmd) {
    def output = "unknown"
    try {
        def workingDir = new File("${project.projectDir}")
        def result = cmd.execute(null, workingDir)
        result.waitFor()
        if (result.exitValue() == 0) {
            output = result.text.trim()
        }
    } catch (e) {
    }
    return output
}

def getCurrentGitBranch() {
    return getCommandResult('git rev-parse --abbrev-ref HEAD')
}

def getCurrentGitCommitSha() {
    return getCommandResult('git rev-parse --short HEAD')
}
